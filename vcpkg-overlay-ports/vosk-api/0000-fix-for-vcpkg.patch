diff --git a/CMakeLists.txt b/CMakeLists.txt
index b754f1e..434be08 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -12,10 +12,34 @@ add_library(vosk
   src/vosk_api.cc
   src/postprocessor.cc
 )
+target_include_directories(vosk PUBLIC $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)
+set_target_properties(vosk PROPERTIES PUBLIC_HEADER src/vosk_api.h)
 
 find_package(kaldi REQUIRED)
-target_link_libraries(vosk PUBLIC kaldi-base kaldi-online2 kaldi-rnnlm fstngram)
+target_link_libraries(vosk PUBLIC kaldi::kaldi-base kaldi::kaldi-online2 kaldi::kaldi-rnnlm OpenFst::fstngram)
 
 include(GNUInstallDirs)
-install(TARGETS vosk DESTINATION ${CMAKE_INSTALL_LIBDIR})
-install(FILES src/vosk_api.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
+install(TARGETS vosk
+  EXPORT vosk-targets
+  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
+  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
+  PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
+)
+
+
+# Export the vosk target for downstream usage
+install(EXPORT vosk-targets
+  NAMESPACE vosk::
+  DESTINATION ${CMAKE_INSTALL_DATADIR}/vosk
+)
+
+# Generate and install vosk-config.cmake
+include(CMakePackageConfigHelpers)
+configure_package_config_file(
+  ${CMAKE_CURRENT_SOURCE_DIR}/cmake/vosk-config.cmake.in
+  ${CMAKE_CURRENT_BINARY_DIR}/vosk-config.cmake
+  INSTALL_DESTINATION ${CMAKE_INSTALL_DATADIR}/vosk
+)
+install(FILES ${CMAKE_CURRENT_BINARY_DIR}/vosk-config.cmake
+  DESTINATION ${CMAKE_INSTALL_DATADIR}/vosk)
diff --git a/cmake/vosk-config.cmake.in b/cmake/vosk-config.cmake.in
new file mode 100644
index 0000000..ac4295b
--- /dev/null
+++ b/cmake/vosk-config.cmake.in
@@ -0,0 +1,6 @@
+@PACKAGE_INIT@
+
+include(CMakeFindDependencyMacro)
+find_dependency(kaldi)
+
+include("${CMAKE_CURRENT_LIST_DIR}/vosk-targets.cmake")
